# 3.3 Specification of Tokens

## Exercise 3.3.1
Consult the language reference manuals to determine (*i*) the sets of characters
that form the input alphabet excluding those that may only appear in character
strings or comments (*ii*) the lexical form of numerical constants and (*iii*)
the lexical form of identifiers for each of the following languages (a) C
(b) C++ (c) C# (d) Fortran (e) Java (f) Lisp (g) SQL.

**Answer:**

a) C

Source: [C language documentation](https://learn.microsoft.com/en-us/cpp/c-language).

*i*:
```
a-z A-Z 0-9 - + * / : = , . ; ( ) [ ] { } ' " ^ & ! ? < > | % ~ _ # \
```
*ii*:
```
number -> integer | floating_point

integer -> decimal optional_integer_suffix
         | octal optional_integer_suffix
         | hexadecimal optional_integer_suffix
decimal -> nonzero_digit | decimal digit
octal -> 0 | octal octal_digit
hexadecimal -> hex_prefix hex_digit | hexadecimal hex_digit
hex_prefix -> 0x | 0X
digit -> 0-9
nonzero_digit -> 1-9
octal_digit -> 0-7
hex_digit -> 0-9 | a-f | A-F

optional_integer_suffix -> ϵ | integer_suffix
integer_suffix -> unsigned_suffix
                | unsigned_suffix long_suffix
                | unsigned_suffix long_long_suffix
                | unsigned_suffix 64_bit_int_suffix
                | long_suffix
                | long_suffix unsigned_suffix
                | long_long_suffix
                | long_long_suffix unsigned_suffix
                | 64_bit_integer_suffix
unsigned_suffix -> u | U
long_suffix -> l | L
long_long_suffix -> ll | LL
64_bit_integer_suffix -> i64 | I64

floating_point -> fraction optional_exponent fp_suffix
                | digit_sequence exponent fp_suffix
fraction -> digit_sequence . digit_sequence
          | . digit_sequence
          | digit_sequence .
exponent -> e sign digit_sequence | E sign digit_sequence
sign -> ϵ | + | -
digit_sequence -> digit | digit_sequence digit
fp_suffix -> ϵ | f | l | F | L
```
*iii*:
```
identifier -> non_digit | identifier non_digit | identifier digit
non_digit -> _ | a-z | A-Z
digit -> 0-9
```

b) C++

Same as C. Source: [C language documentation](https://learn.microsoft.com/en-us/cpp/c-language).

c) C#

Source: [C# standard specification](https://learn.microsoft.com/en-us/dotnet/csharp/specification/overview).

*i*:
Alphabet of C plus `@`, `$` and unicode characters.

*ii*:
```
number -> integer_literal | real_literal

integer_literal -> decimal_integer_literal
                 | hexadecimal_integer_literal
                 | binary_integer_literal
decimal_integer_literal ->
    decimal_digit decorated_decimal_digit* integer_type_suffix?
decorated_decimal_digit -> '_'* decimal_digit
decimal_digit -> '0'-'9'
integer_type_suffix -> 'u' | 'U' | 'l' | 'L' | 'ul' | 'uL' | 'Ul' | 'UL'
                     | 'lu' | 'Lu' | 'lU' | 'LU'
hexadecimal_integer_literal ->
    ('0x' | '0x') decorated_hex_digit+ integer_type_suffix?
decorated_hex_digit -> '_'* hex_digit
hex_digit -> '0'-'9' | 'a'-'f' | 'A'-'F'
binary_integer_literal ->
    ('0b' | '0B') decorated_binary_digit+ integer_type_suffix?
decorated_binary_digit -> '_'* binary_digit
binary_digit -> '0' | '1'

real_literal -> decimal_digit decorated_decimal_digit* '.' decimal_digit
                decorated_decimal_digit* exponent_part? real_type_suffix?
              | '.' decimal_digit decorated_decimal_digit* exponent_part?
                real_type_suffix?
              | decimal_digit decorated_decimal_digit* exponent_part
                real_type_suffix?
              | decimal_digit decorated_decimal_digit* real_type_suffix
exponent_part -> ('e' | 'e') sign? decimal_digit decorated_decimal_digit*
sign -> '+' | '-'
real_type_suffix -> 'f' | 'F' | 'd' | 'D' | 'm' | 'M'
```
*iii*:
```
identifier -> simple_identifier | contextual_keyword | discard_token
discard_token -> _
simple_identifier -> available_identifier | escaped_identifier
available_identifier -> basic_identifier
escaped_identifier -> @ basic_identifier
basic_identifier -> identifier_start_character identifier_part_character*
identifier_start_character -> letter_character | underscore_character
underscore_character -> '_'| '\\u005' [ff] | '\\u0000005' [ff]
identifier_part_character -> letter_character
                           | decimal_digit_character
                           | connecting_character
                           | combining_character
                           | formatting_character
letter_character -> [\p{l}\p{nl}] | unicode_escape_sequence
combining_character -> [\p{mn}\p{mc}] | unicode_escape_sequence
decimal_digit_character -> [\p{nd}] | unicode_escape_sequence
connecting_character -> [\p{pc}] | unicode_escape_sequence
formatting_character -> [\p{cf}] | unicode_escape_sequence
```

d) Fortran

Source: [FORTRAN 77 Language Reference](https://docs.oracle.com/cd/E19957-01/805-4939/index.html).

*i*:
```
a-z A-Z 0-9 _ + − * / : ( ) . = , ‘ $ : ! “ % & ; < > ? $
```
*ii*:
```
number -> integer | real
integer -> sign digits

real -> basic_real | basic_real exponent | integer exponent
basic_real -> sign digits . optional_digits | sign optional_digits . digits
exponent -> E integer

sign -> ϵ | + | -
optional_digits -> ϵ | digits
digits -> digits | digit
digit -> 0-9
```
*iii*:
```
identifier -> letter | identifier letter | identifier digit | identifier _
letter -> a-z | A-Z
digit -> 0-9
```

e) Java

Source: [The Java® Language Specification](https://docs.oracle.com/javase/specs/jls/se7/html/index.html).

*i*:

Alphabet of C plus `@`, `$` and unicode characters.

*ii*:
```
number -> integer | floating_point

integer -> decimal_integer | hex_integer | octal_integer | binary_integer
decimal_integer -> decimal_numeral integer_type
hex_integer -> hex_numeral integer_type
octal_integer -> octal_numeral integer_type
binary_integer -> binary_numeral integer_type
integer_type -> ϵ | l | L

decimal_numeral -> 0
                 | non_zero_digit digits
                 | nonzero_digit
                 | non_zero_digit underscores digits
digits -> digit | digit digit | digit digits_and_underscores digit
digit -> 0 | non_zero_digit
non_zero_digit -> 1-9
digits_and_underscores -> digit_or_underscore
                        | digits_and_underscores digit_or_underscore
digit_or_underscore -> digit | _
underscores -> _ | underscores _

hex_numeral -> 0x hex_digits | 0X hex_digits
hex_digits -> hex_digit
            | hex_digit hex_digit
            | hex_digit hex_digits_and_underscores hex_digit
hex_digit -> 0-9 | a-f | A-F
hex_digits_and_underscores -> hex_digit_or_underscore
                            | hex_digits_and_underscores hex_digit_or_underscore
hex_digit_or_underscore -> hex_digit | _

octal_numeral -> 0 octal_digits | 0 underscores octal_digits
octal_digits -> octal_digit
              | octal_digit octal_digit
              | octal_digit octal_digits_and_underscores octal_digit
octal_digit -> 0-7
octal_digits_and_underscores -> octal_digit_or_underscore
                        | octal_digits_and_underscores octal_digit_or_underscore
octal_digit_or_underscore -> octal_digit | _

binary_numeral -> 0b binary_digits | 0B binary_digits
binary_digits -> binary_digit
               | binary_digit binary digit
               | binary_digit binary_digits_and_underscores binary_digit
binary_digit -> 0 | 1
binary_digits_and_underscores -> binary_digit_or_underscore
                    | binary_digits_and_underscores binary_digit_or_underscore
binary_digit_or_underscore -> binary_digit | _

floating_point_literal -> decimal_floating_point_literal
                        | hexadecimal_floating_point_literal

decimal_floating_point_literal ->
    digits . digits exponent_part_opt floating_point_suffix_opt
    digits . exponent_part_opt floating_point_suffix_opt
    . digits exponent_part_opt floating_point_suffix_opt
    digits exponent_part floating_point_suffix_opt
    digits exponent_part_opt floating_point_suffix
exponent_part_opt -> ϵ | exponent_part
exponent_part -> e signed_integer | E signed_integer
signed_integer -> sign digits
sign -> ϵ | + | -
floating_point_suffix_opt -> ϵ | floating_point_suffix
floating_point_suffix -> f | F | d | D

hexadecimal_floating_point_literal ->
    hex_significand binary_exponent floating_point_suffix_opt
hex_significand -> hex_numeral
                 | hex_numeral .
                 | 0x hex_digits_opt . hex_digits
                 | 0x hex_digits_opt . hex_digits
hex_digits_opt -> ϵ | hex_digits
binary_exponent -> binary_exponent_indicator signed_integer
binary_exponent_indicator -> p | P
```
*iii*:
```
identifier -> identifier_chars
identifier_chars -> java_letter | identifier_chars java_letter_or_digit
java_letter -> a-z | A-Z | _ \u005f | $ | \u0024
java_letter_or_digit -> java_letter | 0-9
```

f) Lisp

Source: [Common Lisp the Language, 2nd Edition](https://www.cs.cmu.edu/Groups/AI/html/cltl/cltl2.html).

*i*:
```
! " # $ % & ' ( ) * + , - . / 0 1 2 3 4 5 6 7 8 9 : ; < = > ?
@ A B C D E F G H I J K L M N O P Q R S T U V W X Y Z [ \ ] ^ _
` a b c d e f g h i j k l m n o p q r s t u v w x y z { | } ~
```
*ii*:
```
number -> integer | ratio | floating_point | complex

integer -> decimal | radix_nn
decimal -> sign digits optional_decimal_point
digits -> digits digit
digit -> 0-9
sign -> ϵ | - | +
optional_decimal_point -> ϵ | .
radix_nn -> radix_specifier sign radix_digits
radix_specifier -> # nn r | # nn R | #b | #o #x
nn -> 2-36
radix_digit -> 0-9 a-z A-Z

ratio -> sign digits / digits | radix_specifier sign radix_digits / radix_digits

floating_point -> sign optional_digits . optional_digits exponent
                | sign optional_digits . optional_digits
                | sign digits exponent
optional_digits -> ϵ | digits
exponent -> exponent_marker sign digits
exponent_marker -> e | s | f | d | l | E | S | F | D | L

complex -> #C(integer integer)
         | #C(floating_point floating_point)
         | #C(ratio floating_point)
         | #C(floating_point ration)
```
*iii*:
```
identifier -> non_escaped | escaped | '|' characters '|'
characters -> any | characters any
any -> non_space | space
escaped_symbol -> \ any
escaped -> non_spaces escaped_symbol | escaped_symbol non_space
non_escaped ->non_spaces+ if not a number
```

g) SQL

Source: [SQL Language Reference](https://docs.oracle.com/en/database/oracle/oracle-database/19/sqlrf/Introduction-to-Oracle-SQL.html).

*i*:
```
0-9 a-z A-Z _ @ # $ + - * / = < > % _ & ' " ( ) [ ] , ; .
```
*ii*:
```
number -> integer -> floating_point
integer -> sign digits
digits -> digits digit
digit -> 0-9
sign -> ϵ | - | +
floating_point -> sign digits . digits optional_exponent optional_type
                | sign digits . optional_exponent optional_type
                | sign . digits optional_exponent optional_type
optional_exponent -> ϵ | exponent
exponent -> exponent_marker sign digits
exponent_marker -> ϵ | E | e
optional_type -> ϵ | f | F | d | D
```
*iii*:
```
identifier -> quoted | non_quoted
quoted -> " any "
any -> any character except " and '\0'
non_quoted -> letter | non_quoted characters
letter -> a-z | A-Z
character -> letter | 0-9 | _ | $ | # | . | @
```

## Exercise 3.3.2
Describe the languages denoted by the following regular expressions:

a) `a(a|b)*a`

**Answer**:
L = {All strings of `a`s and `b`s that start and end with an `a`}.

b) `((ϵ|a)b*)*`

**Answer**:
L = {All strings of `a`s and `b`s including empty string}.

c) `(a|b)*a(a|b)(a|b)`

**Answer**:
L = {All strings of `a`s and `b`s that are at least 3 characters long,
third character from the end is always `a`}.

d) `a*ba*ba*ba*`

**Answer**:
L = {All strings of `a`s and `b`s with exactly three `b` characters}.

e) `(aa|bb)*((ab|ba)(aa|bb)*(ab|ba)(aa|bb)*)*`

**Answer**:
L = {All strings of `a`s and `b`s that have even number of `a`s and `b`s
including empty string}.
